{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "f99b392a-e16b-4b42-8ca8-6a6345fa9219",
   "metadata": {},
   "source": [
    "### Set up the geometry"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "4c80001c-180c-4ee5-9aa8-52cb3fde85c7",
   "metadata": {},
   "outputs": [],
   "source": [
    "@everywhere function MicroKuboSetup(vertices, edges, therm_runtime, T, 𝒽, isRandom)\n",
    "    \n",
    "    if isRandom # initialise entire system in random state\n",
    "        for edge in edges\n",
    "            edge.σ = rand(Bool)\n",
    "        end\n",
    "    else # initialise entire system in ground state\n",
    "        for edge in edges\n",
    "            if sixVertex\n",
    "                edge.σ = vertices[edge.∂[1]].x[1]-vertices[edge.∂[2]].x[1]==0 # gives ~GS ONLY for PBCs on square lattice\n",
    "            else\n",
    "                edge.σ = false\n",
    "            end\n",
    "            edge.D = 0\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    E = zeros(therm_runtime+1) # just set initial energy to zero since we only need the variance\n",
    "    \n",
    "    # thermalise entire system\n",
    "    for t in 1:therm_runtime\n",
    "        E[t+1] = E[t]\n",
    "        for _ in edges\n",
    "            β = rand(eachindex(edges))\n",
    "            ΔE = ΔE_flip(vertices, edges, β, 𝒽)\n",
    "\n",
    "            if ΔE <= 0 || rand(Uniform(0,1)) < exp(-ΔE/T)\n",
    "                edges[β].σ = !edges[β].σ\n",
    "                E[t+1] += ΔE\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    return E[2:end] # cut out initial energy for consistency with other observables\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "b6bec7e7-ca6c-41a0-b50b-92a36473896e",
   "metadata": {},
   "source": [
    "### Single spin-flip dynamics routine "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "1e85b252-f35b-489c-b3d3-4a1e6c5e31ea",
   "metadata": {},
   "outputs": [],
   "source": [
    "@everywhere function MicroKubo(vertices, edges, runtime, 𝒽)\n",
    "    J = zeros(Float64, (length(vertices[1].x), runtime))\n",
    "    \n",
    "    for t in 1:runtime\n",
    "        for _ in edges\n",
    "            β = rand(eachindex(edges))\n",
    "            ΔE = ΔE_flip(vertices, edges, β, 𝒽)\n",
    "            \n",
    "            if ΔE == 0\n",
    "                Δj_β = Δj_flip(vertices, edges, β)\n",
    "                edges[β].σ = !edges[β].σ\n",
    "            \n",
    "                # update x-current\n",
    "                r_β = vertices[edges[β].∂[1]].x - vertices[edges[β].∂[2]].x\n",
    "                for d in 1:length(r_β) # if vector has any axis displacement > 1, normalise to handle PBCs\n",
    "                    r_β[d] /= (abs(r_β[d])>1) ? -abs(r_β[d]) : 1\n",
    "                end\n",
    "                \n",
    "                J[:,t] += r_β * Δj_β # note no factor of 1/2 b/c only sum each edge once\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    return J\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e422ae14-1846-4799-828e-9f39977afe6f",
   "metadata": {},
   "source": [
    "### Double spin-flip dynamics routine"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "5399a261-d15b-4805-aa94-38323f872602",
   "metadata": {},
   "outputs": [],
   "source": [
    "@everywhere function MicroKubo_2flip(vertices, edges, runtime, 𝒽)\n",
    "    J = zeros(Float64, (length(vertices[1].x), runtime))\n",
    "    \n",
    "    for t in 1:runtime\n",
    "        for _ in 1:floor(UInt32, length(edges)/2)\n",
    "            \n",
    "            # propose flips\n",
    "            i = rand(eachindex(vertices)) # shared vertex\n",
    "            𝜷 = sample(vertices[i].δ, 2; replace=true) # two nearest-neighbour spins to flip (in order)\n",
    "            \n",
    "            𝒊 = [edges[𝜷[n]].∂[findfirst(edges[𝜷[n]].∂ .!= i)] for n in 1:2] # outer vertices (but may still coincide)\n",
    "            \n",
    "            ΣA = A(edges, vertices[i]) + A(edges, vertices[𝒊[1]]) + A(edges, vertices[𝒊[2]])\n",
    "            \n",
    "            # calculate overall energy change and current density between the two unshared vertices\n",
    "            ΔE = ΔE_2flip(vertices, edges, 𝜷, 𝒊, 𝒽)\n",
    "            Δj = Δj_2flip(vertices, edges, 𝜷, 𝒊, 𝒽)\n",
    "                \n",
    "            # decide whether to accept and perform the move\n",
    "            #if ΔE == 0 && edges[𝜷[1]].σ!=edges[𝜷[2]].σ && ΣA>0 # energy AND magnetisation conserved AND NO pair diffusion moves (i.e. no particle at central site i)\n",
    "            #if ΔE == 0 && edges[𝜷[1]].σ!=edges[𝜷[2]].σ && ΣA<0 # energy AND magnetisation conserved AND ONLY pair diffusion moves (i.e. no particle at central site i)\n",
    "            if ΔE == 0 && edges[𝜷[1]].σ!=edges[𝜷[2]].σ # energy AND magnetisation conserved\n",
    "            #if ΔE == 0 # energy conserved\n",
    "                \n",
    "                edges[𝜷[1]].σ = !edges[𝜷[1]].σ\n",
    "                edges[𝜷[2]].σ = !edges[𝜷[2]].σ\n",
    "                \n",
    "                # get path of current flow\n",
    "                r_β1 = vertices[i].x - vertices[𝒊[1]].x\n",
    "                for d in 1:length(r_β1) # if vector has any axis displacement > 1, normalise to handle PBCs\n",
    "                    r_β1[d] /= (abs(r_β1[d])>1) ? -abs(r_β1[d]) : 1\n",
    "                end\n",
    "                \n",
    "                r_β2 = vertices[𝒊[2]].x - vertices[i].x\n",
    "                for d in 1:length(r_β2) # if vector has any axis displacement > 1, normalise to handle PBCs\n",
    "                    r_β2[d] /= (abs(r_β2[d])>1) ? -abs(r_β2[d]) : 1\n",
    "                end\n",
    "                \n",
    "                J[:,t] += (r_β1 + r_β2) * Δj # note no factor of 1/2 b/c only sum each pair of sites once\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    return J\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a1ccb97a-adb6-4651-90c0-231ed5c34b63",
   "metadata": {},
   "source": [
    "### Single Simulation Run"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "08480277-9386-46f8-99a5-b7e0b457806e",
   "metadata": {},
   "outputs": [],
   "source": [
    "@everywhere function MKuboSingle(vertices, edges, runtime, therm_runtime, t_therm, t_autocorr, N_blocks, t_cutoff, T, 𝒽)\n",
    "    \n",
    "    Cfun = (E) -> var(E) / T^2 / length(edges)\n",
    "    κfun = (S) -> mean(S) / T^2 / length(edges)\n",
    "    Dfun = (E,S) -> κfun(S) / Cfun(E)\n",
    "    \n",
    "    tmax = runtime-t_therm\n",
    "    \n",
    "    # -- 0. Run Simulation --\n",
    "    E = MicroKuboSetup(vertices, edges, therm_runtime, T, 𝒽, false)\n",
    "    \n",
    "    M = 0\n",
    "    for edge in edges\n",
    "        M += (-1)^edge.σ\n",
    "    end\n",
    "    M /= length(edges)\n",
    "    \n",
    "    bondNumber = 0\n",
    "    maxBondNumber = 0\n",
    "    for vertex in vertices\n",
    "        z = length(vertex.δ)\n",
    "        z₋ = 0\n",
    "        for α in vertex.δ\n",
    "            z₋ += edges[α].σ ? 1 : 0\n",
    "        end\n",
    "        z₊ = z - z₋\n",
    "        \n",
    "        bondNumber += z₊*z₋/2\n",
    "        maxBondNumber += z*(z-1)/2\n",
    "    end\n",
    "    ℙ = bondNumber/maxBondNumber\n",
    "    \n",
    "    if twoFlip\n",
    "        J = MicroKubo_2flip(vertices, edges, runtime, 𝒽)\n",
    "    else\n",
    "        J = MicroKubo(vertices, edges, runtime, 𝒽)\n",
    "    end\n",
    "    \n",
    "    # cut out thermalisation time\n",
    "    J = J[:,t_therm+1:end]\n",
    "    E = E[t_therm+1:end]\n",
    "    \n",
    "    # -- 1. Heat Capacity --\n",
    "    C_μ, C_σ = MyBootstrap([E], Cfun, t_autocorr, N_blocks)\n",
    "    \n",
    "    # -- 2. Thermal Conductivity and Diffusivity--s\n",
    "    κ_μ = 0\n",
    "    κ_v = 0\n",
    "    D_μ = 0\n",
    "    D_v = 0\n",
    "    for τ in 0:t_cutoff\n",
    "        statistic = (τ==0 ? 0.5 : 1.0) .* J[1,:] .* circshift(J[1,:], -τ)\n",
    "        \n",
    "        tmp1, tmp2 = MyBootstrap([statistic[1:end-τ]], κfun, t_autocorr, N_blocks)\n",
    "        κ_μ += tmp1\n",
    "        κ_v += tmp2^2\n",
    "        \n",
    "        tmp1, tmp2 = MyBootstrap([E[1:end-τ], statistic[1:end-τ]], Dfun, t_autocorr, N_blocks)\n",
    "        D_μ += tmp1\n",
    "        D_v += tmp2^2\n",
    "    end\n",
    "    \n",
    "    #push!(testing, [T, 𝒽, IntAutocorrTime([E, J[1,:], J[2,:]])])\n",
    "    \n",
    "    return [κ_μ C_μ D_μ abs.(M) ℙ; κ_v C_σ^2 D_v 0 0]\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "69392f12-9743-4db4-b168-c3b195502789",
   "metadata": {},
   "source": [
    "### Overall simulation routine"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "9e5d76af-ba4d-4236-b19d-e95165655b6e",
   "metadata": {},
   "outputs": [],
   "source": [
    "function MKuboSimulation(vertices, edges, num_histories, runtime, therm_runtime, t_therm, t_autocorr, N_blocks, t_cutoff, T, 𝒽)\n",
    "    \n",
    "    ks = range(1,length(T)*length(𝒽)*num_histories)\n",
    "    args = [[deepcopy(vertices), deepcopy(edges), runtime, therm_runtime, t_therm, t_autocorr, N_blocks, t_cutoff, T[div(div(k-1,num_histories),length(𝒽))+1], 𝒽[rem(div(k-1,num_histories),length(𝒽))+1]] for k=ks]\n",
    "    \n",
    "    function hfun(args)\n",
    "        return MKuboSingle(args...)\n",
    "    end\n",
    "    \n",
    "    \n",
    "    if multiProcess\n",
    "        results = pmap(hfun, args)\n",
    "    else\n",
    "        results = Array{Any}(undef, length(ks))\n",
    "        Threads.@threads for k in ks\n",
    "            results[k] = hfun(args[k])\n",
    "        end\n",
    "    end \n",
    "    \n",
    "    tmp = zeros(2,5,length(T),length(𝒽),num_histories) # rows for mean and stdv of κ,C\n",
    "    for k in ks\n",
    "        ni,h = divrem(k-1,num_histories) .+ (1,1)\n",
    "        n,i = divrem(ni-1,length(𝒽)) .+ (1,1)\n",
    "        \n",
    "        tmp[:,:,n,i,h] = results[k]\n",
    "    end\n",
    "    tmp = sum(tmp, dims=5)\n",
    "    \n",
    "    # average over observables for all histories - okay b/c iid random variables\n",
    "    tmp[2,:,:,:] = sqrt.(tmp[2,:,:,:])\n",
    "    tmp ./= num_histories\n",
    "        \n",
    "    return tmp[1,1,:,:], tmp[1,2,:,:], tmp[1,3,:,:], tmp[1,4,:,:], tmp[1,5,:,:], tmp[2,1,:,:], tmp[2,2,:,:], tmp[2,3,:,:], tmp[2,4,:,:], tmp[2,5,:,:]\n",
    "end"
   ]
  }
 ],
 "metadata": {
  "jupytext": {
   "encoding": "# -*- coding: utf-8 -*-",
   "formats": "ipynb,jl:light"
  },
  "kernelspec": {
   "display_name": "Julia (6 threads) 1.8.2",
   "language": "julia",
   "name": "julia-_6-threads_-1.8"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.8.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
